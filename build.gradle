// Global settings
buildscript {
    ext.kotlin_version = '1.1.2-4'
    repositories {
        jcenter()
        maven() {
            url "https://plugins.gradle.org/m2/"
        }
        maven() {
            url 'https://maven.google.com'
        }
    }
    dependencies {
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.9"
        classpath "gradle.plugin.pl.squirrel:classycle-gradle-plugin:1.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'net.ltgt.apt'
apply plugin: 'pmd'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'

repositories {
    jcenter()
    maven() {
        url "https://plugins.gradle.org/m2/"
    }
}

allprojects {
    targetCompatibility = 1.7
    sourceCompatibility = 1.7
}

// This hack puts testing resource files where we can get to them
task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources/printer"
    into "${buildDir}/classes/test/printer"
}
processTestResources.dependsOn copyTestResources
task cleanTestResources(type: Delete) {
    delete fileTree("${buildDir}/classes/test/printer") {
        include '**/*.bin'
    }
}
test.finalizedBy cleanTestResources

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

checkstyle {
    toolVersion = '7.6'
    configFile file("${projectDir}/src/test/resources/checkstyle.xml")
    sourceSets -= sourceSets.test
}

javadoc {
    options.links "http://google.github.io/guava/releases/20.0/api/docs"
    options.links "http://docs.oracle.com/javase/7/docs/api/"
}

test.finalizedBy jacocoTestReport

jacocoTestReport {
    reports {
        xml.enabled true // coveralls plugin depends on xml format report
        html.enabled true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/AutoValue_**'])
        })
    }
}

// Graph dependencies
task graphDeps(type: Exec) {
    commandLine 'script/graphDeps'
}

configure(graphDeps) {
    group = "Verification"
    description = "Create a dependency graph"
}

check.dependsOn graphDeps

dependencies {
    compileOnly "com.google.auto.value:auto-value:1.2"
    apt "com.google.auto.value:auto-value:1.2"

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    testCompile 'junit:junit:4.12'
}
